Algoritmo "MatrizVotos"
// Aluno: Henrique Generoso da Rosa
Var

   candidatos : vetor [0..4,0..2] de caracter
   menu : inteiro
   l : inteiro
   opcao : caracter
   branco : inteiro
   total : inteiro
   converteVotos : inteiro
   // Variáveis para controle do candidato vencedor
   indiceMaior : inteiro
   nomeMaior : caracter
   chapaMaior : caracter
   //

Inicio
   menu <- 99
   branco <- 0
   total <- 0
   
   para l de 0 ate 4 faca
      escreva("Digite o nome do(a) candidato(a)",l+1,": ")
      leia(candidatos[l,0])
      escreva("Digite a chapa do(a) candidato(a) ",candidatos[l,0],": ")
      leia(candidatos[l,1])
      candidatos[l,2] <- "0"
      limpatela
   fimpara
   enquanto (menu <> 3) faca
      escreval("1 - Votar")
      escreval("2 - Branco")
      escreval("3 - Finalizar")
      leia(menu)
      limpatela
      escolha menu
      caso 1
         escreval("Em qual candidato(a) você deseja votar?")
         para l de 0 ate 4 faca
            escreval(candidatos[l,1]," - ",candidatos[l,0])
         fimpara
         leia(opcao)
         limpatela
         para l de 0 ate 4 faca
            se (opcao = candidatos[l,1]) entao
               converteVotos <- caracpnum(candidatos[l,2])
               converteVotos <- converteVotos + 1
               candidatos[l,2] <- numpcarac(converteVotos)
               total <- total + 1
               escreval("Voto computado!")
            fimse
         fimpara
      caso 2
         branco <- branco + 1
         total <- total + 1
         escreval("Voto em branco computado!")
      caso 3
         indiceMaior <- 0
         nomeMaior <- ""
         chapaMaior <- ""
         para l de 0 ate 4 faca
            se (candidatos[l,0] <> "") entao
               escreval("- Votos do(a) candidato(a) ",candidatos[l,0],": ",candidatos[l,2])
            fimse
            se (caracpnum(candidatos[l,2]) > indiceMaior) entao
               indiceMaior <- caracpnum(candidatos[l,2])
               nomeMaior <- candidatos[l,0]
               chapaMaior <- candidatos[l,1]
            fimse
         fimpara
         escreval("- Votos em branco:",branco)
         escreval("- Total de votos (incluindo votos em branco):",total)
         escreval("- Candidato(a) vencedor(a): ",chapaMaior," - ",nomeMaior)
      fimescolha
   fimenquanto
Fimalgoritmo
